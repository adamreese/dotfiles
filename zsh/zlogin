# ------------------------------------------------------------------------------
# zlogin
# ------------------------------------------------------------------------------

(
  setopt LOCAL_OPTIONS EXTENDED_GLOB
  autoload -U zrecompile

  # Function to determine the need of a zcompile. If the .zwc file
  # does not exist, or the base file is newer, we need to compile.
  # These jobs are asynchronous, and will not impact the interactive shell

  # zcompile the completion cache; siginificant speedup.
  for file in ${ZDOTDIR}/.zcomp^(*.zwc)(.); do
    zrecompile -pq ${file}
  done

  # zcompile .zshrc
  zrecompile -pq ${ZDOTDIR}/.zshrc

  # zcompile all .zsh files in the custom module
  for file in ${ZDOTDIR}/*.zsh(.); do
    zrecompile -pq ${file}
  done

  # zcompile all autoloaded functions
  for file in ${ZDOTDIR}/{completion,functions}/^(*.zwc)(.); do
    zrecompile -pq ${file}
  done

) &!

# ------------------------------------------------------------------------------
# vi:ft=zsh
