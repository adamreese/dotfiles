# ------------------------------------------------------------------------------
# zshrc
# ------------------------------------------------------------------------------

# Zsh functions to load (man zshcontrib).
autoload -Uz zmv

# ------------------------------------------------------------------------------
# zplugin

local -A ZPLGM
ZPLGM[HOME_DIR]="${XDG_CONFIG_HOME}/zplugin"
if [[ ! -f ${ZPLGM[HOME_DIR]}/bin/zplugin.zsh ]]; then
    git clone https://github.com/zdharma/zplugin.git ${ZPLGM[HOME_DIR]}/bin
fi

source ${ZPLGM[HOME_DIR]}/bin/zplugin.zsh
autoload -Uz _zplugin
(( ${+_comps} )) && _comps[zplugin]=_zplugin

zplugin light "zsh-users/zsh-completions"
zplugin light "zsh-users/zsh-history-substring-search"
zplugin light "jocelynmallon/zshmarks"

zplugin ice as"program" make'!' atclone'./direnv hook zsh > zhook.zsh' atpull'%atclone' src"zhook.zsh"
zplugin light direnv/direnv

autoload -Uz compinit; compinit -u
autoload -Uz bashcompinit; bashcompinit

# run compdefs provided by plugins
zplugin cdreplay -q

# ------------------------------------------------------------------------------
# source configuration files

for file (${ZDOTDIR}/*.zsh(N-.)); do
  source $file
done
unset file

# ------------------------------------------------------------------------------
# load prompt

autoload -Uz promptinit && promptinit
# prompt areese
prompt pure

# ------------------------------------------------------------------------------
# hash directories

hash -d --        p=${HOME}/p
hash -d --       dl=${HOME}/Downloads
hash -d --     docs=${HOME}/Documents
hash -d --    gosrc=${GOPATH}/src
hash -d --   cellar=/usr/local/Cellar
hash -d --   goroot=$(go env GOROOT)
hash -d -- dotfiles=${HOME}/.dotfiles


# ------------------------------------------------------------------------------
# path hack
#
# mkdir .git/safe in the root of repositories you trust

path=(
  _scratch/bin
  .git/safe/../../bin

  ${path}
)

# ------------------------------------------------------------------------------
(( ${+ZSH_DEBUG} )) && zprof | less && exit
# ------------------------------------------------------------------------------
# vi:ft=zsh
