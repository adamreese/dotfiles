# ------------------------------------------------------------------------------
# zshrc
# ------------------------------------------------------------------------------

# Zsh functions to load (man zshcontrib).
autoload -Uz zmv

# ------------------------------------------------------------------------------
# zinit

local -A ZINIT
ZINIT[HOME_DIR]="${XDG_DATA_HOME}/zinit"
if [[ ! -f ${ZINIT[HOME_DIR]}/bin/zinit.zsh ]]; then
    git clone https://github.com/zdharma/zinit.git ${ZINIT[HOME_DIR]}/bin
fi

if [[ -f ${ZINIT[HOME_DIR]}/bin/zmodules/Src/zdharma/zplugin.so ]]; then
  module_path+=( ${ZINIT[HOME_DIR]}/bin/zmodules/Src )
  zmodload zdharma/zplugin
fi

source ${ZINIT[HOME_DIR]}/bin/zinit.zsh
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

BOOKMARKS_FILE=${ZDOTDIR}/bookmarks
zinit load 'jocelynmallon/zshmarks'

zinit lucid wait from'gh-r' as'program' mv'direnv* -> direnv' \
  atclone'./direnv hook zsh >! zhook.zsh' atpull'%atclone' \
  pick'direnv' src='zhook.zsh' for \
  direnv/direnv

zinit lucid wait blockf \
  atpull'zinit creinstall -q .' for \
  'zsh-users/zsh-completions'

__bind_history_keys() {
  HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND='fg=yellow,bold'
  HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_NOT_FOUND='bg=red,fg=white,bold'
  HISTORY_SUBSTRING_SEARCH_GLOBBING_FLAGS='i'

  bindkey "$terminfo[kcuu1]" history-substring-search-up
  bindkey "$terminfo[kcud1]" history-substring-search-down
}
zinit lucid wait atload'__bind_history_keys' for \
  'zsh-users/zsh-history-substring-search'


PURE_PROMPT_VICMD_SYMBOL=':'
zstyle ':prompt:pure:git:stash'      show  yes
zstyle ':prompt:pure:git:dirty'      color white
zstyle ':prompt:pure:git:stash'      color blue
zstyle ':prompt:pure:git:arrow'      color white
zstyle ':prompt:pure:path'           color green
zstyle ':prompt:pure:prompt:success' color yellow

zinit lucid pick'async.zsh' src'pure.zsh' for \
  'sindresorhus/pure'

# ------------------------------------------------------------------------------
# source configuration files

for file (${ZDOTDIR}/*.zsh(N-.)); do
  source $file
done
unset file

# ------------------------------------------------------------------------------
# load completion

autoload -Uz compinit
if [[ -n ${ZDOTDIR:-$HOME}/.zcompdump(Nm-20) ]]; then
  compinit -C
else
  compinit
fi

# ------------------------------------------------------------------------------
# hash directories

hash -d --        p=${HOME}/p
hash -d --       dl=${HOME}/Downloads
hash -d --     docs=${HOME}/Documents
hash -d --    gosrc=${GOPATH}/src
hash -d --   cellar=/usr/local/Cellar
hash -d --   goroot=$(go env GOROOT)
hash -d -- dotfiles=${HOME}/.dotfiles


# ------------------------------------------------------------------------------
# path hack
#
# mkdir .git/safe in the root of repositories you trust

path=(
  _scratch/bin
  .git/safe/../../bin

  ${HOME}/.local/bin(N-/)

  /usr/local/opt/ruby/bin(N-/)
  ${HOME}/.cargo/bin(N-/)
  ${path}
)

# ------------------------------------------------------------------------------
(( ${+ZSH_DEBUG} )) && zprof | less && exit
# ------------------------------------------------------------------------------
# vi:ft=zsh
