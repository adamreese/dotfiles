# ------------------------------------------------------------------------------
# zshrc
# ------------------------------------------------------------------------------

# Zsh functions to load (man zshcontrib).
autoload -Uz zmv

zmodules=(zshmarks history-substring-search)
fpath=(${${zmodules}:+${ZDOTDIR}/modules/${^zmodules}/functions(/FN)} ${fpath})
autoload -Uz ${ZDOTDIR}/modules/${^zmodules}/functions/*(N:t)

autoload -Uz compinit; compinit
autoload -Uz bashcompinit; bashcompinit

# ------------------------------------------------------------------------------
# source configuration files

for file (${ZDOTDIR}/*.zsh(N-.)); do
  source $file
done
unset file

# ------------------------------------------------------------------------------
# load plugins

for file (${ZDOTDIR}/modules/${^zmodules}/init.zsh(N-.)); do
  source $file
done
unset file

# ------------------------------------------------------------------------------
# load prompt

if [[ ! ${TERM} == (linux|*bsd*|dumb) ]]; then
  autoload -Uz promptinit && promptinit
  # prompt areese
  prompt pure
fi

# ------------------------------------------------------------------------------
# hash directories

hash -d --     docs=${HOME}/Documents
hash -d -- dotfiles=${HOME}/.dotfiles
hash -d --    gosrc=${GOPATH}/src
hash -d --   cellar=/usr/local/Cellar

# ------------------------------------------------------------------------------
# path hack
#
# mkdir .git/safe in the root of repositories you trust

path=(
  _scratch/bin
  .git/safe/../../bin
  ${path}
)

# ------------------------------------------------------------------------------
(( ${+ZSH_DEBUG} )) && zprof | less
# ------------------------------------------------------------------------------
# vi:ft=zsh
