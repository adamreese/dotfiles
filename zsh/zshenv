# ------------------------------------------------------------------------------
# zshenv
# ------------------------------------------------------------------------------
[[ -n $ZSH_DEBUG ]] && zmodload zsh/zprof

# XDG Base Directory
# ------------------------------------------------------------------------------
# https://specifications.freedesktop.org/basedir-spec/latest
: ${XDG_CACHE_HOME:=$HOME/.cache}
: ${XDG_CONFIG_HOME:=$HOME/.config}
: ${XDG_DATA_HOME:=$HOME/.local/share}
: ${XDG_RUNTIME_DIR:=/run/user/${UID}}
: ${XDG_RUNTIME_HOME:=$HOME/.tmp}

typeset -gxm 'XDG_*'

: ${ZDOTDIR:=${HOME}/.dotfiles/zsh}

ZSH_CACHE_DIR="${XDG_CACHE_HOME}/zsh"
[[ ! -d ${ZSH_CACHE_DIR} ]] || mkdir -p "${ZSH_CACHE_DIR}"

# application specific xdg
: ${DOCKER_CONFIG:=${XDG_CONFIG_HOME}/docker}
: ${GLIDE_HOME:=${XDG_CACHE_HOME}/glide}
: ${GNUPGHOME:=${XDG_CONFIG_HOME}/gnupg}
: ${HELM_HOME:=${XDG_CONFIG_HOME}/helm}
: ${PSQLRC:=${XDG_CONFIG_HOME}/pg/psqlrc}
: ${PSQL_HISTORY:=$XDG_CACHE_HOME/pg/psql_history}
: ${BOOKMARKS_FILE:=${XDG_CONFIG_HOME}/bookmarks}

typeset -gx DOCKER_CONFIG GLIDE_HOME GNUPGHOME HELM_HOME PSQLRC PSQL_HISTORY BOOKMARKS_FILE

export YARN_CACHE_FOLDER="${XDG_CACHE_HOME}/yarn"

# Path
# ------------------------------------------------------------------------------
typeset -gU cdpath fpath manpath path

# load completion functions
fpath=(
  ${ZDOTDIR}/{completion,functions}(N-/)
  ${ZDOTDIR}/modules/*/{completion,functions}(N-/)
  /usr/local/share/zsh/site-functions(N-/)
  /usr/local/share/zsh-completions(N-/)
  ${fpath}
)
autoload -Uz ${ZDOTDIR}/functions/*(N:t)

path=(
  ${HOME}/.dotfiles/bin(N-/)
  ${HOME}/.local/bin(N-/)

  # GNU Coreutils
  /usr/local/opt/coreutils/libexec/gnubin(N-/)
  /usr/local/{bin,sbin}
  ${path}
)

manpath=(
  # GNU Coreutils
  /usr/local/opt/coreutils/libexec/gnuman(N-/)
  ${manpath}
)

# https://blog.packagecloud.io/eng/2017/02/21/set-environment-variable-save-thousands-of-system-calls/
typeset -gx TZ=:/etc/localtime

# ------------------------------------------------------------------------------
# vi:ft=zsh
